#Область СлужебныйПрограммныйИнтерфейс

#Область РасчетБонусов

Функция ПолучитьЦенуНоменклатурыПоВидуЦены(Номенклатура, ВидЦены, ДатаДокумента) Экспорт

	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ДатаДокумента);

	Запрос = Новый Запрос;

	Если ИспользуетсяЦенообразование25 Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Цены.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(
		|			&Дата,
		|			Номенклатура = &Номенклатура
		|				И ВидЦены = &ВидЦены) КАК Цены";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Цены.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&Дата,
		|			Номенклатура = &Номенклатура
		|				И ВидЦены = &ВидЦены) КАК Цены";
	КонецЕсли;

	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	Запрос.УстановитьПараметр("Дата", ДатаДокумента);

	Результат = Запрос.Выполнить().Выбрать();

	Если Результат.Следующий() Тогда
		Возврат Результат.Цена;
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область НачислениеБонусов

Функция НачислитьБонусы(ДокументСсылка, СуммаБонусов) Экспорт
	
	Если СуммаБонусов = 0 Тогда
		Возврат Новый Структура("Текст, Успешно", НСтр("ru = 'Сумма бонусов равна нулю. Начисление не выполнено'"), Ложь);
	КонецЕсли;
	
	Возврат ЗаписатьДвиженияБонусов(ДокументСсылка, СуммаБонусов, ВидДвиженияНакопления.Приход);

КонецФункции

#КонецОбласти 

#Область СписаниеБонусов

Функция ПолучитьДоступныеБонусы(Партнер) Экспорт

	// Бонусы становятся доступны через 20 дней
	КоличествоДней = 20;
	ГраницаДаты = НачалоДня(ТекущаяДатаСеанса() - КоличествоДней * 86400);

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(Остатки.СуммаБонусовОборот) КАК СуммаБонусовОборот
	|ИЗ
	|	РегистрНакопления.БонусыПокупателей.ОстаткиИОбороты(, , Регистратор, , Покупатель = &Покупатель) КАК Остатки
	|ГДЕ
	|	Остатки.Период <= &ГраницаДаты";

	Запрос.УстановитьПараметр("ГраницаДаты", ГраницаДаты);
	Запрос.УстановитьПараметр("Покупатель", Партнер);

	Результат = Запрос.Выполнить().Выбрать();

	Если Результат.Следующий() Тогда
		Возврат Результат.СуммаБонусовОборот;
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции 

Функция ОтправитьСМСКод(Телефон, Текст) Экспорт
	
	API_ID = Константы.API_ID_SMSRU.Получить();
	
	Если ПустаяСтрока(API_ID) Тогда
		СообщитьПользователю(НСтр("ru = 'Не задан API ID для отправки СМС. Обратитесь к администратору'"));
		Возврат Ложь;
	КонецЕсли;
	
	// Формируем тело запроса
	ТелоЗапроса =
	"api_id=" + КодироватьСтроку(API_ID,  СпособКодированияСтроки.КодировкаURL) +
	"&to="    + КодироватьСтроку(Телефон, СпособКодированияСтроки.КодировкаURL) +
	"&msg="   + КодироватьСтроку(Текст,   СпособКодированияСтроки.КодировкаURL) +
	"&json=1";
	
	Попытка
		// Создаем HTTPЗапрос
		ЗапросHTTP = Новый HTTPЗапрос("/sms/send");
		ЗапросHTTP.Заголовки = Новый Соответствие;
		ЗапросHTTP.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
		ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса, "utf-8");
		
		// Создаем соединение с сервером sms.ru
		Соединение = Новый HTTPСоединение("sms.ru", 443, , , , 30, Истина, );
		
		// Выполняем POST запрос, передаем объект HTTPЗапрос
		Ответ = Соединение.ВызватьHTTPМетод("POST", ЗапросHTTP);
		
		// Проверяем код HTTP-ответа
		Если Ответ.КодСостояния <> 200 Тогда
			СообщитьПользователю(НСтр("ru = 'Ошибка HTTP при отправке СМС: код '") + Ответ.КодСостояния);
			Возврат Ложь;
		КонецЕсли;
		
		// Читаем тело ответа как строку и разбираем JSON
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТекстОтвета);
		Результат = ПрочитатьJSON(ЧтениеJSON, Истина);
		ЧтениеJSON.Закрыть();
		
	Исключение
		СообщитьПользователю(НСтр("ru = 'Ошибка при попытке отправки СМС: '") + ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;

		// Проверяем общий статус ответа от сервиса
		Если Результат["status"] = "OK" И Результат["status_code"] = 100 Тогда
			
			// Проходим по каждому элементу в блоке sms
			Для Каждого СтрSMS Из Результат["sms"] Цикл
				НомерТелефона = СтрSMS.Ключ;
				ДанныеSMS     = СтрSMS.Значение;
				
				// Сначала проверяем сам API-статус по этому номеру
				Если ДанныеSMS["status"] = "OK" И ДанныеSMS["status_code"] = 100 Тогда
					// Сообщение принято и отправлено
					Продолжить;
				Иначе
					// Ошибка на уровне API для данного номера
					СообщитьПользователю(НСтр("ru = 'Ошибка API при отправке СМС'"));
					Возврат Ложь;
				КонецЕсли;
			КонецЦикла;
			
			// Если все номера прошли проверки
			Возврат Истина;
			
		Иначе
			// Ошибка самого сервиса sms.ru
			СообщитьПользователю(НСтр("ru = 'Ошибка на уровне сервиса СМС: код '") + Формат(Результат["status_code"]));
			Возврат Ложь;
		КонецЕсли;
		
КонецФункции

Функция СписатьБонусы(ДокументСсылка, БонусыКСписанию) Экспорт
 	
	Возврат ЗаписатьДвиженияБонусов(ДокументСсылка, БонусыКСписанию, ВидДвиженияНакопления.Расход);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

Процедура НачислитьБонусыРегламентно() Экспорт

    // Получим дату последнего начисления
    ДатаНачала = ПолучитьДатуПоследнегоНачисления();

    // Текущая дата — верхняя граница
    ДатаОкончания = ТекущаяДатаСеанса();

    // Запрос по тем документам, где еще не было начислений
    Запрос = Новый Запрос;
    Запрос.Текст =
    "ВЫБРАТЬ
    |   Обороты.Регистратор КАК Регистратор,
    |   СУММА(Обороты.СуммаБонусовПриход) КАК СуммаБонусовПриход
    |ПОМЕСТИТЬ ВТ_НачисленныеБонусы
    |ИЗ
    |   РегистрНакопления.БонусыПокупателей.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК Обороты
    |СГРУППИРОВАТЬ ПО
    |   Обороты.Регистратор;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ
    |   ДопРеквизиты.Ссылка КАК Партнер
    |ПОМЕСТИТЬ ВТ_ПартнерыБонуснойПрограммы
    |ИЗ
    |   Справочник.Партнеры.ДополнительныеРеквизиты КАК ДопРеквизиты
    |ГДЕ
    |   ДопРеквизиты.Свойство.ИдентификаторДляФормул = ""УчаствуетВБонуснойПрограмме""
    |   И ДопРеквизиты.Значение = ИСТИНА;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ
    |   Реализация.Партнер КАК Покупатель,
    |   Реализация.Ссылка КАК Регистратор
    |ИЗ
    |   Документ.РеализацияТоваровУслуг КАК Реализация
    |       ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НачисленныеБонусы КАК ВТ_НачисленныеБонусы
    |       ПО Реализация.Ссылка = ВТ_НачисленныеБонусы.Регистратор
    |       ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПартнерыБонуснойПрограммы КАК Участники
    |       ПО Реализация.Партнер = Участники.Партнер
    |ГДЕ
    |   Реализация.Дата >= &ДатаНачала
    |   И Реализация.Дата <= &ДатаОкончания
    |   И ЕСТЬNULL(ВТ_НачисленныеБонусы.СуммаБонусовПриход, 0) = 0
    |   И Реализация.ПометкаУдаления = ЛОЖЬ";

    Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
    Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);

    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		Попытка
			ДокументСсылка = Выборка.Регистратор;
			ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			
			СуммаБонусов = РассчитатьСуммуБонусов(ДокументОбъект, ДокументОбъект.Дата);
			Если ЗначениеЗаполнено(СуммаБонусов) И СуммаБонусов > 0 Тогда
				НачислитьБонусы(ДокументСсылка, СуммаБонусов);
			КонецЕсли;
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			ЗаписьЖурналаРегистрации("НачислитьБонусыРегламентно", УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
			Продолжить;
		КонецПопытки;
	КонецЦикла;

    // Обновляем дату последнего начисления
    СохранитьДатуПоследнегоНачисления(ДатаОкончания);

КонецПроцедуры 

Функция ПолучитьДатуПоследнегоНачисления() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыНачисленияБонусов.ТипОбъекта КАК ТипОбъекта,
		|	ДатыНачисленияБонусов.ДатаПоследнегоНачисления КАК ДатаПоследнегоНачисления
		|ИЗ
		|	РегистрСведений.ДатыНачисленияБонусов КАК ДатыНачисленияБонусов";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипОбъекта = "Реализация" Тогда
            Возврат Выборка.ДатаПоследнегоНачисления;
        КонецЕсли;
	КонецЦикла;

	// Если нет записей, вернем фиксированную дату
    Возврат Дата(2024, 7, 7);

КонецФункции
 
Процедура СохранитьДатуПоследнегоНачисления(Дата) Экспорт
	
	Запись = РегистрыСведений.ДатыНачисленияБонусов.СоздатьНаборЗаписей();
    Запись.Отбор.ТипОбъекта.Установить("Реализация");
    Запись.Очистить();

    НоваяСтрока = Запись.Добавить();
    НоваяСтрока.ТипОбъекта = "Реализация";
    НоваяСтрока.ДатаПоследнегоНачисления = Дата;

    Запись.Записать();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Функция: ЕстьНачислениеБонусов
// Назначение:
//   Проверяет наличие движений "Приход" по регистратору в регистре накопления
//   БонусыПокупателей, т.е. определяет, были ли бонусы начислены по документу.
//
// Параметры:
//   ДокументСсылка – ДокументСсылка. Ссылка на документ, по которому нужно
//   проверить факт начисления бонусов.
//
// Возвращаемое значение:
//   Булево. Возвращает Истина, если по документу уже есть движение "Приход".
//   В противном случае возвращает Ложь.
//
// Использование:
//   Используется в формах документов и обработках для предотвращения повторного
//   начисления бонусов по одному и тому же документу.
////////////////////////////////////////////////////////////////////////////////
Функция ЕстьНачислениеБонусов(ДокументСсылка) Экспорт

	Набор = РегистрыНакопления.БонусыПокупателей.СоздатьНаборЗаписей();
    Набор.Отбор.Регистратор.Установить(ДокументСсылка);
    Набор.Прочитать();

    Для Каждого Строка Из Набор Цикл
        Если Строка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
            Возврат Истина;
        КонецЕсли;
    КонецЦикла;

    Возврат Ложь;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Функция: ЕстьСписаниеБонусов
// Назначение:
//   Проверяет наличие движений "Расход" по регистратору в регистре накопления
//   БонусыПокупателей, т.е. определяет, были ли списаны бонусы по документу.
//
// Параметры:
//   ДокументСсылка – ДокументСсылка. Ссылка на документ, по которому нужно
//   проверить факт списания бонусов.
//
// Возвращаемое значение:
//   Булево. Возвращает Истина, если по документу уже есть движение "Расход".
//   В противном случае возвращает Ложь.
//
// Использование:
//   Используется в формах документов и обработках для предотвращения повторного
//   списания бонусов по одному и тому же документу.
////////////////////////////////////////////////////////////////////////////////
Функция ЕстьСписаниеБонусов(ДокументСсылка) Экспорт
	
	Набор = РегистрыНакопления.БонусыПокупателей.СоздатьНаборЗаписей();
    Набор.Отбор.Регистратор.Установить(ДокументСсылка);
    Набор.Прочитать();

    Для Каждого Строка Из Набор Цикл
        Если Строка.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
            Возврат Истина;
        КонецЕсли;
    КонецЦикла;

    Возврат Ложь;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Функция: ЗаписатьДвиженияБонусов
// Назначение:
//   Записывает движение по регистру накопления БонусыПокупателей:
//   - Приход — начисление бонусов;
//   - Расход — списание бонусов.
//
// Особенности:
//   При списании используется блокировка данных по покупателю и транзакция —
//   для исключения одновременного списания бонусов из нескольких касс или процессов.
//   При начислении блокировка не используется, поскольку одновременное начисление
//   вручную и по регламентному заданию маловероятно.
//
// Параметры:
//   ДокументСсылка – ДокументСсылка. Документ-источник движения.
//   СуммаБонусов   – Число. Сумма бонусов для записи (положительное число).
//   ВидДвижения    – Вид движения накопления (Приход или Расход).
//
// Возвращаемое значение:
//   Структура. Поля:
//     - Текст    – строка сообщения;
//     - Успешно  – булево, признак успешности операции.
//
// Использование:
//   Вызывается при выполнении операции начисления/списания бонусов.
////////////////////////////////////////////////////////////////////////////////
Функция ЗаписатьДвиженияБонусов(ДокументСсылка, СуммаБонусов, ВидДвижения) Экспорт

	ДокОбъект = ДокументСсылка.ПолучитьОбъект();

	Если ВидДвижения = ВидДвиженияНакопления.Расход Тогда
		
		НачатьТранзакцию();
		Попытка

			// Установим блокировку на покупателя
			Блокировка = Новый БлокировкаДанных;
			Элемент = Блокировка.Добавить("РегистрНакопления.БонусыПокупателей");
			Элемент.Режим = РежимБлокировкиДанных.Исключительный;
			Элемент.УстановитьЗначение("Покупатель", ДокОбъект.Партнер);
			Блокировка.Заблокировать();

			// Проверим остаток уже после блокировки
			Доступные = ПолучитьДоступныеБонусы(ДокОбъект.Партнер);
			Если Доступные < СуммаБонусов Тогда
				ОтменитьТранзакцию();
				Возврат Новый Структура("Текст, Успешно", НСтр("ru = 'Недостаточно бонусов для списания. Доступно: '") + Доступные, Ложь);
			КонецЕсли;

			// Проверим, не было ли уже списания
			Если ЕстьСписаниеБонусов(ДокументСсылка) Тогда
				ОтменитьТранзакцию();
				Возврат Новый Структура("Текст, Успешно", НСтр("ru = 'Бонусы уже были списаны по этому документу'"), Ложь);
			КонецЕсли;

			// Добавим движение
			Набор = РегистрыНакопления.БонусыПокупателей.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(ДокументСсылка);
			Набор.Прочитать();

			Движение = Набор.Добавить();
			Движение.Покупатель = ДокОбъект.Партнер;
			Движение.СуммаБонусов = СуммаБонусов;
			Движение.Регистратор = ДокументСсылка;
			Движение.Период = ДокОбъект.Дата;
			Движение.ВидДвижения = ВидДвижения;

			Набор.Записать();

			ЗафиксироватьТранзакцию();

			Возврат Новый Структура("Текст, Успешно", СтрШаблон(НСтр("ru = 'Сумма списанных бонусов: %1'"), СуммаБонусов), Истина);

		Исключение
			ОтменитьТранзакцию();
			Возврат Новый Структура("Текст, Успешно", "Ошибка при списании бонусов: " + ОписаниеОшибки(), Ложь);
		КонецПопытки;

	Иначе // Начисление — без блокировок

		Если ЕстьНачислениеБонусов(ДокументСсылка) Тогда
			Возврат Новый Структура("Текст, Успешно", НСтр("ru = 'Бонусы уже были начислены по этому документу'"), Ложь);
		КонецЕсли;

		Набор = РегистрыНакопления.БонусыПокупателей.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(ДокументСсылка);
		Набор.Прочитать();

		Движение = Набор.Добавить();
		Движение.Покупатель = ДокОбъект.Партнер;
		Движение.СуммаБонусов = СуммаБонусов;
		Движение.Регистратор = ДокументСсылка;
		Движение.Период = ДокОбъект.Дата;
		Движение.ВидДвижения = ВидДвижения;

		Набор.Записать();

		Возврат Новый Структура("Текст, Успешно", СтрШаблон(НСтр("ru = 'Сумма начисленных бонусов: %1'"), СуммаБонусов), Истина);

	КонецЕсли;

КонецФункции

Функция ПолучитьНомерТелефонаПартнера(ПартнерСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|    ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
	|ИЗ
	|    Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|ГДЕ
	|    ПартнерыКонтактнаяИнформация.Ссылка = &Партнер";
	
	Запрос.УстановитьПараметр("Партнер", ПартнерСсылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.НомерТелефона;
	КонецЕсли;
	
	Возврат ""; // Телефон не найден

КонецФункции

Функция ПолучитьВидыЦенДляПокупателя(Покупатель) Экспорт
	
	// Виды цен по умолчанию: РРЦ и Розница
	ВидЦеныПоУмолчаниюПервая  = Справочники.ВидыЦен.НайтиПоРеквизиту("Идентификатор", "06РРЦ");
    ВидЦеныПоУмолчаниюВторая  = Справочники.ВидыЦен.НайтиПоРеквизиту("Идентификатор", "05Розница");
	
	// Если не удалось определить виды цен по умолчанию — бонусы начислены не будут
    Если НЕ ЗначениеЗаполнено(ВидЦеныПоУмолчаниюПервая)
    ИЛИ НЕ ЗначениеЗаполнено(ВидЦеныПоУмолчаниюВторая) Тогда
        Возврат Неопределено;
    КонецЕсли;
	
	// Пытаемся найти индивидуальные настройки видов цен для покупателя
    Запрос = Новый Запрос;
	Запрос.Текст =
        "ВЫБРАТЬ
        |	ЦеныДляРасчетаБонусов.ПерваяЦена КАК ПерваяЦена,
        |	ЦеныДляРасчетаБонусов.ВтораяЦена КАК ВтораяЦена
        |ИЗ
        |	РегистрСведений.ЦеныДляРасчетаБонусов КАК ЦеныДляРасчетаБонусов
        |ГДЕ
        |	ЦеныДляРасчетаБонусов.Покупатель = &Покупатель";
    Запрос.УстановитьПараметр("Покупатель", Покупатель);

    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
	
	// Если найдены индивидуальные виды цен и оба заполнены — используем их
    Если Выборка.Следующий() Тогда
        Если ЗначениеЗаполнено(Выборка.ПерваяЦена) И ЗначениеЗаполнено(Выборка.ВтораяЦена) Тогда
            Возврат Новый Структура("ПерваяЦена,ВтораяЦена", Выборка.ПерваяЦена, Выборка.ВтораяЦена);
        КонецЕсли;
    КонецЕсли;
	
	// Если индивидуальные настройки отсутствуют — используются виды цен по умолчанию (РРЦ – Розница)
    Возврат Новый Структура("ПерваяЦена,ВтораяЦена", ВидЦеныПоУмолчаниюПервая, ВидЦеныПоУмолчаниюВторая);

КонецФункции 

Функция РассчитатьСуммуБонусов(ДокументОбъект, Дата) Экспорт
	
	// Проверим участие партнера в бонусной программе
	Если НЕ ПартнерУчаствуетВБонуснойПрограмме(ДокументОбъект.Партнер) Тогда
		Возврат 0;
	КонецЕсли;
	
	// Если в документе нет строк товаров — выходим без расчета
	Если ДокументОбъект.Товары.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СуммаБонусов = 0;
	
	// Получаем виды цен для расчета бонусов (индивидуальные или по умолчанию)
	ВидыЦен = ПолучитьВидыЦенДляПокупателя(ДокументОбъект.Партнер);
	Если ВидыЦен = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	ПерваяЦенаВид = ВидыЦен.ПерваяЦена;
	ВтораяЦенаВид = ВидыЦен.ВтораяЦена;
	
	Для Каждого Стр Из ДокументОбъект.Товары Цикл
		
		ПерваяЦена = БонусыВызовСервера.ПолучитьЦенуНоменклатурыПоВидуЦены(Стр.Номенклатура, ПерваяЦенаВид, Дата);
		ВтораяЦена = БонусыВызовСервера.ПолучитьЦенуНоменклатурыПоВидуЦены(Стр.Номенклатура, ВтораяЦенаВид, Дата);
		
		// Если хотя бы одна из цен для строки товара не получена — строка исключается из расчета бонусов
		Если НЕ ЗначениеЗаполнено(ПерваяЦена) ИЛИ НЕ ЗначениеЗаполнено(ВтораяЦена) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПерваяЦена > ВтораяЦена Тогда
			СуммаБонусов = СуммаБонусов + ((ПерваяЦена - ВтораяЦена) * Стр.Количество);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СуммаБонусов;
	
КонецФункции 

Функция ПартнерУчаствуетВБонуснойПрограмме(Партнер) Экспорт

	Если НЕ ЗначениеЗаполнено(Партнер) Тогда
		Возврат Ложь;
	КонецЕсли;

	Для каждого Строка Из Партнер.ДополнительныеРеквизиты Цикл
		Если ЗначениеЗаполнено(Строка.Свойство)
			И Строка.Свойство.ИдентификаторДляФормул = "УчаствуетВБонуснойПрограмме" Тогда

			Возврат Строка.Значение = Истина;

		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции 

&НаСервере
Функция ПолучитьЗначенияНастроек() Экспорт
	
	Результат = Новый Структура;

	Результат.Вставить("APIID", Константы.API_ID_SMSRU.Получить());
	Результат.Вставить("Процент", Константы.ПроцентСписанияБонусов.Получить());
	Результат.Вставить("ИдентификацияПриНачислении", Константы.ИдентификацияПриНачислении.Получить());
	Результат.Вставить("ИдентификацияПриСписании", Константы.ИдентификацияПриСписании.Получить());

	Возврат Результат;

КонецФункции


// Выводит сообщение пользователю
//
// Параметры:
//	ТекстСообщенияПользователю - Строка - Текст выводимого сообщения
//
Процедура СообщитьПользователю(ТекстСообщенияПользователю) Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщенияПользователю;
	Сообщение.Сообщить();
КонецПроцедуры

#КонецОбласти
