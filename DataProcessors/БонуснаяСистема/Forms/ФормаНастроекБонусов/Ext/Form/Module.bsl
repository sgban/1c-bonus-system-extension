
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗначенияНастроек = ПолучитьЗначенияНастроек(); 
	
	ЭтотОбъект.API_ID_SMSRU = ЗначенияНастроек.APIID;
	ЭтотОбъект.ПроцентСписанияБонусов = ЗначенияНастроек.Процент;
	ЭтотОбъект.ИспользоватьИдентификациюПриНачислении = ЗначенияНастроек.ИдентификацияПриНачислении;
	ЭтотОбъект.ИспользоватьИдентификациюПриСписании = ЗначенияНастроек.ИдентификацияПриСписании;
	
	ЗагрузитьДанныеВТаблицуЗначений();
	
	УстановитьДоступностьОбщихНастроек(Ложь);
	УстановитьДоступностьИндивидуальныхНастроек(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОбщиеНастройки(Команда)
	УстановитьДоступностьОбщихНастроек(Истина);
КонецПроцедуры

&НаСервере
Процедура СохранитьОбщиеНастройкиНаСервере(APIID, Процент, ИдентификацияНачисление, ИдентификацияСписание)

    ЗаписатьЗначенияОбщихНастроек(APIID, Процент, ИдентификацияНачисление, ИдентификацияСписание);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьОбщиеНастройки(Команда)

    СохранитьОбщиеНастройкиНаСервере(
        ЭтотОбъект.API_ID_SMSRU,
        ЭтотОбъект.ПроцентСписанияБонусов,
        ЭтотОбъект.ИспользоватьИдентификациюПриНачислении,
        ЭтотОбъект.ИспользоватьИдентификациюПриСписании
    );
    
    УстановитьДоступностьОбщихНастроек(Ложь);

КонецПроцедуры


&НаКлиенте
Процедура ИзменитьИндивидуальныеНастройки(Команда)
	УстановитьДоступностьИндивидуальныхНастроек(Истина);
КонецПроцедуры

&НаСервере
Процедура СохранитьИндивидуальныеНастройкиНаСервере()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Покупатель", Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	Таблица.Колонки.Добавить("ПерваяЦена", Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	Таблица.Колонки.Добавить("ВтораяЦена", Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	
	Для Каждого Стр Из ТаблицаИндивидуальныхНастроекРасчета Цикл
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Покупатель = Стр.Покупатель;
		НоваяСтрока.ПерваяЦена = Стр.ПерваяЦена;
		НоваяСтрока.ВтораяЦена = Стр.ВтораяЦена;
	КонецЦикла;
	
	СохранитьНастройкиРСНаСервере(Таблица);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИндивидуальныеНастройки(Команда)
	
	СохранитьИндивидуальныеНастройкиНаСервере();
	УстановитьДоступностьИндивидуальныхНастроек(Ложь);
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьЗначенияНастроек()	
	Результат = Новый Структура;
	Результат.Вставить("APIID", Константы.API_ID_SMSRU.Получить());
	Результат.Вставить("Процент", Константы.ПроцентСписанияБонусов.Получить());
	Результат.Вставить("ИдентификацияПриНачислении", Константы.ИдентификацияПриНачислении.Получить());
	Результат.Вставить("ИдентификацияПриСписании", Константы.ИдентификацияПриСписании.Получить());
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьОбщихНастроек(Доступность)
	
	ЭтотОбъект.Элементы.API_ID_SMSRU.Доступность = Доступность;
	ЭтотОбъект.Элементы.ПроцентСписанияБонусов.Доступность = Доступность;
	ЭтотОбъект.Элементы.ИспользоватьИдентификациюПриНачислении.Доступность = Доступность;
	ЭтотОбъект.Элементы.ИспользоватьИдентификациюПриСписании.Доступность = Доступность;
	
	ЭтотОбъект.Элементы.СохранитьОбщиеНастройки.Доступность = Доступность;
	ЭтотОбъект.Элементы.ИзменитьОбщиеНастройки.Доступность = НЕ Доступность;
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьДоступностьИндивидуальныхНастроек(Доступность)
	
	ЭтотОбъект.Элементы.ТаблицаИндивидуальныхНастроекРасчета.Доступность = Доступность;
	ЭтотОбъект.Элементы.СохранитьИндивидуальныеНастройки.Доступность = Доступность;
	ЭтотОбъект.Элементы.ИзменитьИндивидуальныеНастройки.Доступность = НЕ Доступность;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначенияОбщихНастроек(APIID, Процент, ИдентификацияНачисление, ИдентификацияСписание)

    Константы.API_ID_SMSRU.Установить(APIID);
    Константы.ПроцентСписанияБонусов.Установить(Процент);
    Константы.ИдентификацияПриНачислении.Установить(ИдентификацияНачисление);
    Константы.ИдентификацияПриСписании.Установить(ИдентификацияСписание);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеВТаблицуЗначений()
	
	ТаблицаФормы = ЭтотОбъект.ТаблицаИндивидуальныхНастроекРасчета;
	ТаблицаФормы.Очистить(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦеныДляРасчетаБонусов.Покупатель КАК Покупатель,
	|	ЦеныДляРасчетаБонусов.ПерваяЦена КАК ПерваяЦена,
	|	ЦеныДляРасчетаБонусов.ВтораяЦена КАК ВтораяЦена
	|ИЗ
	|	РегистрСведений.ЦеныДляРасчетаБонусов КАК ЦеныДляРасчетаБонусов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Покупатель";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаФормы.Добавить();
		НоваяСтрока.Покупатель = Выборка.Покупатель;
		НоваяСтрока.ПерваяЦена = Выборка.ПерваяЦена;
		НоваяСтрока.ВтораяЦена = Выборка.ВтораяЦена;
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура СохранитьНастройкиРСНаСервере(Таблица)
	
	Для Каждого Стр Из Таблица Цикл
		Если НЕ ЗначениеЗаполнено(Стр.ПерваяЦена) Тогда
			ВызватьИсключение 
			"Для покупателя """ + Стр.Покупатель + """ не задана первая цена.";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Стр.ВтораяЦена) Тогда
			ВызватьИсключение 
			"Для покупателя """ + Стр.Покупатель + """ не задана вторая цена.";
		КонецЕсли;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Набор = РегистрыСведений.ЦеныДляРасчетаБонусов.СоздатьНаборЗаписей();
		Набор.Прочитать();
		Набор.Очистить();
		
		Для Каждого Стр Из Таблица Цикл
			НоваяСтрока = Набор.Добавить();
			НоваяСтрока.Покупатель = Стр.Покупатель;
			НоваяСтрока.ПерваяЦена = Стр.ПерваяЦена;
			НоваяСтрока.ВтораяЦена = Стр.ВтораяЦена;
		КонецЦикла;
		
		Набор.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации("СохранитьНастройкиРСНаСервере", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
		ВызватьИсключение;  
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти