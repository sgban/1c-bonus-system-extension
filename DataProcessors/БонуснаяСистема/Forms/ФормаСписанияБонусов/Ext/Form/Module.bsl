#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Настройки = БонусыВызовСервера.ПолучитьЗначенияНастроек();
	
	ЭтотОбъект.Элементы.ГруппаСМС.Видимость = Настройки.ИдентификацияПриНачислении;
	ЭтотОбъект.Элементы.СписатьБонусы.Доступность = НЕ Настройки.ИдентификацияПриНачислении;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ДокументСсылка") Тогда
		
		БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'Эту форму можно открыть только из документа'"));
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	ДокументСсылка   = Параметры.ДокументСсылка;
	Партнер          = Параметры.Партнер;
	СуммаДокумента   = Параметры.СуммаДокумента;
	
	ДоступныеБонусы = БонусыВызовСервера.ПолучитьДоступныеБонусы(Партнер); 
	
	ПроцентСписанияБонусов = Константы.ПроцентСписанияБонусов.Получить();
	
	Процент = ПроцентСписанияБонусов / 100;
	
	// Вычисляем максимально допустимую сумму
	МаксимальноВозможное = СуммаДокумента * Процент;

	Если МаксимальноВозможное > ДоступныеБонусы Тогда
		МаксимальноВозможное = ДоступныеБонусы;
	КонецЕсли;

	БонусыКСписанию = МаксимальноВозможное;
	//ЭтотОбъект.Элементы.БонусыКСписанию.МаксимальноеЗначение = БонусыКСписанию;
    ЭтотОбъект.Элементы.БонусыКСписанию.МаксимальноеЗначение = ДоступныеБонусы;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ОтправитьКодНаСервере()
	
	Телефон = БонусыВызовСервера.ПолучитьНомерТелефонаПартнера(Партнер);
	Если ПустаяСтрока(Телефон) Тогда
		БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'У покупателя не указан номер телефона'"));
		Возврат;
	КонецЕсли;
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	КодПодтверждения = Формат(ГСЧ.СлучайноеЧисло(1000, 9999), "ЧГ=0");
	ЭтотОбъект.КодПодтверждения = КодПодтверждения; // сохраняем код в реквизите формы
	
	Текст = "Код подтверждения: " + КодПодтверждения;
	
	Успешно = БонусыВызовСервера.ОтправитьСМСКод(Телефон, Текст);
	
	Если Успешно Тогда
		БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'Код отправлен'"));
	Иначе
		БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'Не удалось отправить код'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКод(Команда)
	ОтправитьКодНаСервере();
КонецПроцедуры

&НаСервере
Функция ПроверитьКодНаСервере()
    Если ПустаяСтрока(ЭтотОбъект.КодПодтверждения) Тогда
        БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'Код подтверждения не был отправлен'"));
        Возврат Ложь;
    КонецЕсли;
    
    Если ЭтотОбъект.КодИзСМС = ЭтотОбъект.КодПодтверждения Тогда
        БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'Код подтверждён'"));
        Возврат Истина;
    Иначе
        БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'Неверный код. Проверьте и попробуйте ещё раз'"));
        Возврат Ложь;
    КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ПроверитьКод(Команда)
	Результат = ПроверитьКодНаСервере();
    ЭтотОбъект.Элементы.СписатьБонусы.Доступность = Результат;
КонецПроцедуры

&НаСервере
Функция СписатьБонусыНаСервере()
	Возврат БонусыВызовСервера.СписатьБонусы(ДокументСсылка, БонусыКСписанию);
КонецФункции

&НаКлиенте
Процедура СписатьБонусы(Команда)
	
	Если НЕ ЗначениеЗаполнено(БонусыКСписанию) Тогда
		БонусыВызовСервера.СообщитьПользователю(НСтр("ru = 'Укажите сумму бонусов для списания'"));
		Возврат;
	КонецЕсли; 
	
	РезультатСписания = СписатьБонусыНаСервере(); // Возвращает структура: Текст, Успешно

	Если НЕ РезультатСписания.Успешно Тогда
		ПоказатьПредупреждение(, РезультатСписания.Текст, 0, "Списание бонусов");
		Возврат; 
	КонецЕсли;

	// Все прошло успешно
	Оповещение = Новый ОписаниеОповещения("ПослеСообщенияБонусыСписаны", ЭтотОбъект);
	ПоказатьПредупреждение(Оповещение, РезультатСписания.Текст, 0, "Списание бонусов");
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеСообщенияБонусыСписаны(Знач Параметр) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("СписаноБонусов", БонусыКСписанию);
	Закрыть(Результат);

КонецПроцедуры

#КонецОбласти
